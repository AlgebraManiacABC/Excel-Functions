AGE=LAMBDA(birthdate,TRUNC((TODAY()-birthdate)/365.25))
CONTAINS=LAMBDA(find_text,within_text,IF(IFERROR(SEARCH(find_text,within_text),FALSE),TRUE,FALSE))
COUNTNOTBLANK=LAMBDA(range,ROWS(range)*COLUMNS(range)-COUNTBLANK(range))
FLATTEN=LAMBDA(array,remove_blanks,by_column,LET(f,TOCOL(array,0,by_column),IF(remove_blanks,FILTER(f,f<>""),f)))
GETCOLUMNINDICES=LAMBDA(height,width,FLOOR.MATH(SEQUENCE(height*width,,1,1/height)))
GETRANGEH=LAMBDA(start,cell_count,OFFSET(start,0,0,0,cell_count))
GETRANGEV=LAMBDA(start,cell_count,OFFSET(start,0,0,cell_count))
GETRANGEX=LAMBDA(start,height,width,OFFSET(start,0,0,height,width))
GETROWINDICES=LAMBDA(height,width,MOD(SEQUENCE(height*width,,0),height)+1)
IFBLANK=LAMBDA(test,if_blank,IF(test="",if_blank,test))
IFBLANK_ELSE=LAMBDA(test,if_blank,else,IF(test="",if_blank,else))
IFNOTBLANK=LAMBDA(test,if_not_blank,IF(test="",test,if_not_blank))
IFNOTBLANK_ELSE=LAMBDA(test,if_not_blank,else,IF(test="",else,if_not_blank))
IFPROPER=LAMBDA(text,if_true,if_false,IF(EXACT(PROPER(text),text),if_true,if_false))
IFTEXTZERO=LAMBDA(test,if_zero,IF(test="0",if_zero,test))
IFZERO=LAMBDA(test,if_zero,IF(test=0,if_zero,test))
IFZERO_ELSE=LAMBDA(test,if_zero,else,IF(test=0,if_zero,else))
MAXWORDS=LAMBDA(range,MAX(WORDCOUNT(range)))
ONLYEVEN=LAMBDA(array,INDEX(array,MAKEARRAY(ROWS(array)/2,1,LAMBDA(row,col,row*2))))
ONLYODD=LAMBDA(array,INDEX(array,MAKEARRAY(ROWS(array)/2,1,LAMBDA(row,col,(row*2)-1))))
ORDERED_SET_OF=LAMBDA(array,SORT(SET_OF(array)))
REMOVEBLANK=LAMBDA(array,LET(f,TOCOL(array),FILTER(f,f<>"")))
RUNNINGSUM=LAMBDA(range,SCAN(0,range,LAMBDA(x,y,x+y)))
SET_OF=LAMBDA(array,UNIQUE(FLATTEN(array,TRUE,TRUE)))
STRINGS_TO_WORD_ARRAY=LAMBDA(text_range,LET(x,text_range,maxwords,MAXWORDS(x),IFERROR(CHOOSECOLS(TEXTBEFORE(TEXTAFTER(" "&x," ",SEQUENCE(,MAX(LEN(x))))&" "," "),SEQUENCE(,maxwords,1)),"")))
STRINGS_TO_WORDARRAY=LAMBDA(range,MAKEARRAY(ROWS(range),MAXWORDS(range),LAMBDA(row,col,IFERROR(INDEX(TEXTSPLIT(INDEX(range,row)," "),col),""))))
WORDCOUNT=LAMBDA(text,IF(LEN(text)=0,0,LEN(TRIM(text))-LEN(SUBSTITUTE(text," ",""))+1))

Task-specific formulas:
VE=LAMBDA(text,VALID_ELEVATION(text))
VALID_ELEVATION=LAMBDA(text,IFBLANK_ELSE(text,"",AND(VALID_SESSION(text),VALID_SLASH(text),VALID_YEAR(text))))
VALID_SESSION=LAMBDA(text,IFBLANK_ELSE(text,1,XLOOKUP(LEFT(text,1),{"G","P","0","1","2","3","4","5","6","7","8","9"},{1,1,1,1,1,1,1,1,1,1,1,1},0)))
VALID_SLASH=LAMBDA(text,IF(RIGHT(LEFT(text,2),1)="/",1,0))
VALID_YEAR=LAMBDA(text,1*NOT(NOT(IFERROR(VALUE(RIGHT(text,4)),0))))
