AGE=LAMBDA(birthdate,TRUNC((TODAY()-birthdate)/365.25))
COUNTNOTBLANK=LAMBDA(range,ROWS(range)*COLUMNS(range)-COUNTBLANK(range))
IFBLANK=LAMBDA(test,if_blank,IF(test="",if_blank,test))
IFBLANK_ELSE=LAMBDA(test,if_blank,else,IF(test="",if_blank,else))
IFNOTBLANK=LAMBDA(test,if_not_blank,IF(test="",test,if_not_blank))
IFNOTBLANK_ELSE=LAMBDA(test,if_not_blank,else,IF(test="",else,if_not_blank))
IFPROPER=LAMBDA(text,if_true,if_false,IF(EXACT(PROPER(text),text),if_true,if_false))
MAXWORDS=LAMBDA(range,MAX(WORDCOUNT(range)))
STRINGS_TO_WORDARRAY=LAMBDA(text_range,LET(x,text_range,maxwords,MAXWORDS(x),IFERROR(CHOOSECOLS(TEXTBEFORE(TEXTAFTER(" "&x," ",SEQUENCE(,MAX(LEN(x))))&" "," "),SEQUENCE(,maxwords,1)),"")))
WORDCOUNT=LAMBDA(text,LEN(TRIM(text))-LEN(SUBSTITUTE(text," ",""))+1)

Task-specific formulas:
VE=LAMBDA(text,VALID_ELEVATION(text))
VALID_ELEVATION=LAMBDA(text,IFBLANK_ELSE(text,"",AND(VALID_SESSION(text),VALID_SLASH(text),VALID_YEAR(text))))
VALID_SESSION=LAMBDA(text,IFBLANK_ELSE(text,1,XLOOKUP(LEFT(text,1),{"G","P","0","1","2","3","4","5","6","7","8","9"},{1,1,1,1,1,1,1,1,1,1,1,1},0)))
VALID_SLASH=LAMBDA(text,IF(RIGHT(LEFT(text,2),1)="/",1,0))
VALID_YEAR=LAMBDA(text,1*NOT(NOT(IFERROR(VALUE(RIGHT(text,4)),0))))
